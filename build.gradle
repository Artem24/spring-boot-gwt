buildscript {
    ext {
        springBootVersion = '1.3.0.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'nebula.provided-base'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    main.java.srcDir "src/main/java"
    main.resources.srcDir "src/main/resources"
    test.java.srcDir "src/test/java"
    test.groovy.srcDir "src/test/groovy"
    test.resources.srcDir "src/test/resources"
}

dependencies {
    provided 'com.google.gwt:gwt-user:2.8.0-beta1',
            'com.google.gwt:gwt-dev:2.8.0-beta1',
            'org.fusesource.restygwt:restygwt:2.1.1'

    compile 'org.springframework.boot:spring-boot-starter-data-jpa',
            'org.springframework.boot:spring-boot-starter-web',
            'javax.ws.rs:javax.ws.rs-api:2.0.1'

    runtime 'com.h2database:h2'

    testCompile 'org.springframework.boot:spring-boot-starter-test',
            'org.codehaus.groovy:groovy-all:2.4.5',
            'org.spockframework:spock-core:1.0-groovy-2.4',
            'org.spockframework:spock-spring:1.0-groovy-2.4'
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

task compileGwt(dependsOn: classes, type: JavaExec) {
    ext.buildDir = "${project.buildDir}/gwt"
    ext.extraDir = "${project.buildDir}/extra"

    inputs.source sourceSets.main.java.srcDirs
    inputs.dir sourceSets.main.output.resourcesDir
    outputs.dir buildDir

    doFirst {
        file(buildDir).mkdirs()
    }

    main = 'com.google.gwt.dev.Compiler'

    classpath {
        [
                sourceSets.main.java.srcDirs,           // Java source
                sourceSets.main.output.resourcesDir,    // Generated resources
                sourceSets.main.output.classesDir,      // Generated classes
                sourceSets.main.compileClasspath,       // Deps
        ]
    }

    args = [
            'com.codecrafters.SpringBootGwt',
            '-war', buildDir,
            '-logLevel', 'INFO',
            '-localWorkers', '4',
            '-compileReport',
            '-extra', extraDir,
            '-style', 'OBF',
            '-optimize', '7' // 0=none, 9=max
    ]

    maxHeapSize = '512M'
}

jar.dependsOn compileGwt
jar {
    baseName = 'spring-boot-gwt'
    version = '0.0.1-SNAPSHOT'

    into('static') {
        from compileGwt.buildDir
    }
}

// gets executed by heroku
task stage {
    dependsOn build
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}
